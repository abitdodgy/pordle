defmodule Pordle.CLI do
  @moduledoc """                                             

  ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó  ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó ‚ñà‚ñà‚ïó     ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó
  ‚ñà‚ñà‚ïî‚ïê‚ïê‚ñà‚ñà‚ïó‚ñà‚ñà‚ïî‚ïê‚ïê‚ïê‚ñà‚ñà‚ïó‚ñà‚ñà‚ïî‚ïê‚ïê‚ñà‚ñà‚ïó‚ñà‚ñà‚ïî‚ïê‚ïê‚ñà‚ñà‚ïó‚ñà‚ñà‚ïë     ‚ñà‚ñà‚ïî‚ïê‚ïê‚ïê‚ïê‚ïù
  ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïî‚ïù‚ñà‚ñà‚ïë   ‚ñà‚ñà‚ïë‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïî‚ïù‚ñà‚ñà‚ïë  ‚ñà‚ñà‚ïë‚ñà‚ñà‚ïë     ‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó  
  ‚ñà‚ñà‚ïî‚ïê‚ïê‚ïê‚ïù ‚ñà‚ñà‚ïë   ‚ñà‚ñà‚ïë‚ñà‚ñà‚ïî‚ïê‚ïê‚ñà‚ñà‚ïó‚ñà‚ñà‚ïë  ‚ñà‚ñà‚ïë‚ñà‚ñà‚ïë     ‚ñà‚ñà‚ïî‚ïê‚ïê‚ïù  
  ‚ñà‚ñà‚ïë     ‚ïö‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïî‚ïù‚ñà‚ñà‚ïë  ‚ñà‚ñà‚ïë‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïî‚ïù‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ïó
  ‚ïö‚ïê‚ïù      ‚ïö‚ïê‚ïê‚ïê‚ïê‚ïê‚ïù ‚ïö‚ïê‚ïù  ‚ïö‚ïê‚ïù‚ïö‚ïê‚ïê‚ïê‚ïê‚ïê‚ïù ‚ïö‚ïê‚ïê‚ïê‚ïê‚ïê‚ïê‚ïù‚ïö‚ïê‚ïê‚ïê‚ïê‚ïê‚ïê‚ïù

  Welcome to Pordle CLI. Type `:help` for help. Type `:quit` to quit. Good luck!
  """
  alias Pordle.Game

  @commands %{
    ":quit" => "Quits the game",
    ":help" => "Available commands"
  }

  @doc """
  Entry point for the game.

  """
  def main(argv) do
    IO.puts(@moduledoc)

    {:ok, server} =
      argv
      |> parse_args()
      |> start_server()

    server
    |> Pordle.get_game()
    |> render_board()

    receive_command(server)
  end

  defp parse_args(args) do
    {options, _, _} =
      OptionParser.parse(args,
        switches: [puzzle_size: :integer, player: :string, moves_allowed: :integer, puzzle: :string]
      )

    options
  end

  defp start_server(opts), do: Pordle.create_game(opts)

  defp render_board(%Game{board: board, moves_made: moves_made, moves_allowed: moves_allowed}) do
    IO.puts("< üòÄ > Your board after #{highlight(moves_made)} round(s):\n")

    Enum.each(board, fn row ->
      Enum.each(row, &draw_cell/1)
      IO.puts("\n")
    end)

    board
    |> List.flatten()
    |> Enum.reject(fn {char, _type} -> is_nil(char) end)
    |> Enum.uniq_by(fn {char, _type} -> char end)
    |> then(fn board ->
      unless Enum.empty?(board) do
        IO.puts("< üòÄ > Your keyboard after #{highlight(moves_made)} round(s):\n")
        Enum.each(board, &draw_cell/1)
        IO.puts("\n")
      end
    end)

    IO.puts("< üòÄ > You have #{highlight(moves_allowed - moves_made)} guess(es) remaning.\n")
  end

  defp receive_command(server) do
    IO.gets("< üßê > Type your guess and press return: ")
    |> String.trim()
    |> execute_command(server)
  end

  def execute_command(":quit" = cmd, _server) do
    @commands
    |> Map.get(cmd)
    |> IO.puts()
  end

  def execute_command(":help" = cmd, _server) do
    @commands
    |> Map.get(cmd)
    |> IO.puts()
  end

  def execute_command(guess, server) do
    IO.puts("\n< ü§î > You guessed #{highlight(guess)}\n")
    play_move(server, guess)
  end

  defp play_move(server, guess) do
    server
    |> Pordle.put_player_move(guess)
    |> case do
      {:ok, %Game{result: :won, moves_made: moves_made} = game} ->
        render_board(game)
        IO.puts("< ü§© > Congratulations, you won in #{highlight(moves_made)} guess(es)! üèÜ\n")
        IO.puts("< üòÄ > Game over.\n")

      {:ok, %Game{result: :lost} = game} ->
        render_board(game)
        IO.puts("< üò≠ > Bad luck, you lost! üí©\n")
        IO.puts("< üòñ > Game over.\n")

      {:ok, game} ->
        render_board(game)
        receive_command(server)

      {:error, :invalid_move} ->
        IO.puts("< üôÑ > The word #{highlight(guess)} is not the correct length.\n")
        receive_command(server)

      {:error, :word_not_found} ->
        IO.puts("< ü§≠ > The word #{highlight(guess)} was not found in the dictionary.\n")
        receive_command(server)

      {:error, :game_over} ->
        IO.puts("< ü§î > Game over.\n")
    end
  end

  defp highlight(char) do
    IO.ANSI.light_red() <> "#{char}" <> IO.ANSI.reset()
  end

  defp draw_cell({char, type}) do
    char = String.upcase("#{char}")

    char =
      case type do
        :hit ->
          IO.ANSI.green_background() <> " #{char} "
        :miss ->
          IO.ANSI.color_background(2, 2, 2) <> " #{char} "
        :empty ->
          IO.ANSI.color_background(4, 4, 4) <> "\s\s\s"
        :nearly ->
          IO.ANSI.light_cyan_background() <> " #{char} "
      end <> IO.ANSI.reset() <> " "

    IO.write(char)
  end
end
